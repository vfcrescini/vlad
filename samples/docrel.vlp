/*
 * document release example
 * Vino Fernando Crescini  <jcrescin@cit.uws.edu.au>
 */

/* entity declaration */

entity sub sci, pat_off;
entity acc own, read, write, review, pat_ok, pat_reject, release;
entity obj doc;

/* interval declaration */
interval today, yesterday, tomorrow;

/* initial facts */

initially holds(sci, own, doc, today);
initially holds(sci, read, doc, today);
initially holds(sci, write, doc, today);

/* updates */

request(SS1, SS2, OS1)
  causes
    holds(SS2, review, OS1, today) &&
    !holds(SS1, write, OS1, today)
  if
    holds(SS1, own, OS1, today) &&
    holds(SS1, write, OS1, today);

get_approval(SS1, SS2, OS1)
  causes
    holds(SS1, pat_ok, OS1, today) &&
    !holds(SS2, review, OS1, today)
  if
    holds(SS2, review, OS1, today) &&
    holds(SS1, own, OS1, today);

get_rejection(SS1, SS2, OS1)
  causes
    holds(SS1, pat_reject, OS1, today) &&
    !holds(SS2, review, OS1, today)
  if
    holds(SS2, review, OS1, today) &&
    holds(SS1, own, OS1, today);

release_doc(SS1, OS1)
  causes
    holds(SS1, release, OS1, today) &&
    !holds(SS1, pat_ok, OS1, today)
  if
    holds(SS1, pat_ok, OS1, today);

revise_doc(SS1, OS1)
  causes
    holds(SS1, write, OS1, today)
  if
    holds(SS1, pat_reject, OS1, today);
