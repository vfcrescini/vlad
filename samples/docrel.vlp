/*
 * document release example
 * Vino Fernando Crescini  <jcrescin@cit.uws.edu.au>
 */

ident sub sci, pat_off;
ident acc own, read, write, review, pat_ok, pat_reject, release;
ident obj doc;

/* initial facts */

initially holds(sci, own, doc);
initially holds(sci, read, doc);
initially holds(sci, write, doc);

/* updates */

request(X, Y, Z)
  causes
    holds(Z, review, Y) &&
    !holds(X, write, Y)
  if
    holds(X, own, Y) &&
    holds(X, write, Y);

get_approval(X, Y, Z)
  causes
    holds(X, pat_ok, Y) &&
    !holds(Z, review, Y)
  if
    holds(Z, review, Y) &&
    holds(X, own, Y);

get_rejection(X, Y, Z)
  causes
    holds(X, pat_reject, Y) &&
    !holds(Z, review, Y)
  if
    holds(Z, review, Y) &&
    holds(X, own, Y);

release_doc(X, Y)
  causes
    holds(X, release, Y) &&
    !holds(X, pat_ok, Y)
  if
    holds(X, pat_ok, Y);

revise_doc(X, Y)
  causes
    holds(X, write, Y)
  if
    holds(X, pat_reject, Y);
