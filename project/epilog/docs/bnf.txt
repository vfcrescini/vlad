-------------------------------------------------------------------------------
BNF

Vino Crescini  <jcrescin@cit.uws.edu.au>
-------------------------------------------------------------------------------
Started 2002 Jul 08
Updated 2003 Apr 10
-------------------------------------------------------------------------------

<start>                 : <program>

<program>               : <init> <body> <destroy>

<init>                  :

<destroy>               :

<body>                  : <first-part> | <last-part> |

<first-part>            : <ident-stmt-list> | 
                          <ident-stmt-list> <last-part>

<last-part>             : <other-stmt-list>

<ident-stmt-list>       : <ident-stmt> | <ident-stmt-list> <ident-stmt> 
                        
<other-stmt-list>       : <other-stmt> | <other-stmt-list> <other-stmt>
                        
<other-stmt>            : <initial-stmt> | <trans-stmt> | <query-stmt>

<ident-stmt>            : <ident> <ident-declaration> <semicolon>

<ident-declaration>     : <sub-ident-decl> | <obj-ident-decl> | acc-ident-decl> |
                          <sub-grp-ident-decl> | <obj-grp-ident-decl> |
                          <acc-grp-ident-decl>

<sub-ident-decl>        : <sub-type> <sub-ident-list>

<acc-ident-decl>        : <acc-type> <acc-ident-list>

<obj-ident-decl>        : <obj-type> <obj-ident-list>

<sub-grp-ident-decl>    : <sub-grp-type> <sub-grp-ident-list>

<acc-grp-ident-decl>    : <acc-grp-type> <acc-grp-ident-list>

<obj-grp-ident-decl>    : <obj-grp-type> <obj-grp-ident-list>

<sub-ident-list>        : <identifier> | <sub-ident-list> <comma> <identifier>

<obj-ident-list>        : <identifier> | <obj-ident-list> <comma> <identifier>

<acc-ident-list>        : <identifier> | <acc-ident-list> <comma> <identifier>

<sub-grp-ident-list>    : <identifier> | <sub-grp-ident-list> <comma> <identifier>

<obj-grp-ident-list>    : <identifier> | <obj-grp-ident-list> <comma> <identifier>

<acc-grp-ident-list>    : <identifier> | <acc-grp-ident-list> <comma> <identifier>

<initial-stmt>          : <initially> <ground-exp> <semicolon>
                        
<trans-stmt>            : <trans> <identifier> <trans-var-def> <causes>
                          <comp-exp> <if-clause> <semicolon>

<if-clause>             : NIL | <if> <comp-exp>
                       
<trans-var-def>         : <open-parent> <close-parent> |
                          <open-parent> <trans-var-list> <close-parent> 
                        
<trans-var-list>        : <identifier> | <trans-var-list> <comma> <identifier>

<query-stmt>            : <query-clause> <after-clause> <semicolon> |
                          <query-clause> <semicolon>
                        
<query-clause>          : <query> <ground-exp>
                        
<after-clause>          : <after> <trans-ref-list>
                        
<trans-ref-list>        : <trans-ref-def> | <trans-ref-list> <comma>
                          <trans-ref-def>
                        
<trans-ref-def>         : <identifier> <open-parent>
                          <trans-ref-ident-args> <close-parent>

<trans-ref-ident-args>  : | <trans-ref-ident-list>
                        
<trans-ref-ident-list>  : <identifier> | <trans-ref-ident-list> <comma> 
                          <identifier>

<ground-exp>            : <ground-exp> <logical-op> <ground-boolean-atom> |
                          <ground-boolean-atom>
                        
<ground-boolean-atom>   : <not> <ground-atom-exp> | <ground-atom-exp>
                        
<ground-atom-exp>       : <ground-holds-atom> | <ground-subst-atom> | 
                          <ground-memb-atom>
                        
<ground-holds-atom>     : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<ground-subst-atom>     : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<ground-memb-atom>      : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<comp-exp>              : <comp-exp> <logical-op> <comp-boolean-atom> |
                          <comp-boolean-atom>

<comp-boolean-atom>     : <not> <comp-atom-exp> | <comp-atom-exp>

<comp-atom-exp>         : <comp-holds-atom> | <comp-subst-atom> |
                          <comp-memb-atom>

<comp-holds-atom>       : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<comp-subst-atom>       : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<comp-memb-atom>        : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<logical-op>            : <and>
                        
<identifier>            : [a-zA-Z][a-zA-Z0-9_]{0,127}

<and>                   : &&
                        
<not>                   : !
                        
<holds>                 : holds
                        
<member>                : memb
                        
<subset>                : subst
                        
<transform>             : trans

<initially>             : initially
                        
<causes>                : causes
                        
<if>                    : if
                        
<query>                 : query
                        
<after>                 : after

<ident>                 : ident

<sub-type>              : sub

<obj-type>              : obj

<acc-type>              : acc

<sub-grp-type>          : sub-grp

<obj-grp-type>          : obj-grp

<acc-grp-type>          : acc-grp
                        
<underscore>            : _

<open-parent>           : (

<close-parent>          : )

<comma>                 : ,
                        
<semicolon>             : ;
