-------------------------------------------------------------------------------
VLAD: V Logic-based Access-control Database
BNF

Vino Crescini  <jcrescin@cit.uws.edu.au>
-------------------------------------------------------------------------------
Started 2002 Dec 08
-------------------------------------------------------------------------------

<start>                 : <program>

<program>               : <init> <body> <destroy>

<init>                  :

<destroy>               :

<body>                  : <ident-section> <initial-section> 
                          <constraint-section> <trans-section> 
                          <query-section>

<ident-section>         : <nil> | <ident-stmt-list>

<initial-section>       : <nil> | <inital-stmt-list>

<constraint-section>    : <nil> | <constraint-stmt-list>

<trans-section>         : <nil> | <trans-stmt-list>

<query-section>         : <nil> | <query-stmt-list>

<ident-stmt-list>       : <ident-stmt> | <ident-stmt-list> <ident-stmt> 

<initial-stmt-list>     : <initial-stmt> | <initial-stmt-list> <initial-stmt>

<constraint-stmt-list>  : <constraint-stmt> | <constraint-stmt-list>
                          <constraint-stmt>

<trans-stmt-list>       : <trans-stmt> | <trans-stmt-list> <trans-stmt>

<query-stmt-list>       : <query-stmt> | <query-stmt-list> <query-stmt>
                        
<ident-stmt>            : <ident> <ident-declaration> <semicolon>
                       
<ident-declaration>     : <sub-ident-decl> | <obj-ident-decl> | acc-ident-decl> |
                          <sub-grp-ident-decl> | <obj-grp-ident-decl> |
                          <acc-grp-ident-decl>

<sub-ident-decl>        : <sub-type> <sub-ident-list>

<acc-ident-decl>        : <acc-type> <acc-ident-list>

<obj-ident-decl>        : <obj-type> <obj-ident-list>

<sub-grp-ident-decl>    : <sub-grp-type> <sub-grp-ident-list>

<acc-grp-ident-decl>    : <acc-grp-type> <acc-grp-ident-list>

<obj-grp-ident-decl>    : <obj-grp-type> <obj-grp-ident-list>

<sub-ident-list>        : <identifier> | <sub-ident-list> <comma> <identifier>

<obj-ident-list>        : <identifier> | <obj-ident-list> <comma> <identifier>

<acc-ident-list>        : <identifier> | <acc-ident-list> <comma> <identifier>

<sub-grp-ident-list>    : <identifier> | <sub-grp-ident-list> <comma> <identifier>

<obj-grp-ident-list>    : <identifier> | <obj-grp-ident-list> <comma> <identifier>

<acc-grp-ident-list>    : <identifier> | <acc-grp-ident-list> <comma> <identifier>

<initial-stmt>          : <initially> <ground-exp> <semicolon>

<constraint-stmt>       : <implies-stmt> | <always-stmt>
                        
<implies-stmt>          : <ground-exp> <implies> <ground-exp> <with-clause> 
                          <semicolon>

<with-clause>           : <nil> | <with> <absence> <ground-exp>

<always-stmt>           : <always> <ground-exp> <semicolon>

<trans-stmt>            : <identifier> <trans-var-def> <causes> <comp-exp> 
                          <if-clause> <semicolon>

<if-clause>             : <nil> | <if> <comp-exp>

<trans-var-def>         : <open-parent> <close-parent> |
                          <open-parent> <trans-var-list> <close-parent> 
                        
<trans-var-list>        : <identifier> | <trans-var-list> <comma> <identifier>

<query-stmt>            : (is> <ground-exp> <after-clause> <semicolon>
                        
<after-clause>          : <nil> | <after> <trans-ref-list>
                        
<trans-ref-list>        : <trans-ref-def> | <trans-ref-list> <comma>
                          <trans-ref-def>
                        
<trans-ref-def>         : <identifier> <open-parent>
                          <trans-ref-ident-args> <close-parent>

<trans-ref-ident-args>  : <nil> | <trans-ref-ident-list>
                        
<trans-ref-ident-list>  : <identifier> | <trans-ref-ident-list> <comma> 
                          <identifier>

<ground-exp>            : <ground-exp> <logical-op> <ground-boolean-atom> |
                          <ground-boolean-atom>
                        
<ground-boolean-atom>   : <not> <ground-atom> | <ground-atom>
                        
<ground-atom>           : <ground-holds-atom> | <ground-subst-atom> | 
                          <ground-memb-atom> | <logical-atom>
                        
<ground-holds-atom>     : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<ground-subst-atom>     : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<ground-memb-atom>      : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<comp-exp>              : <comp-exp> <logical-op> <comp-boolean-atom> |
                          <comp-boolean-atom>

<comp-boolean-atom>     : <not> <comp-atom> | <comp-atom>

<comp-atom>             : <comp-holds-atom> | <comp-subst-atom> |
                          <comp-memb-atom> | <logical-atom>

<comp-holds-atom>       : <holds> <open-parent> <identfier> <comma>
                          <identifier> <comma> <identifier> <close-parent>
 
<comp-subst-atom>       : <subset> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>
                        
<comp-memb-atom>        : <member> <open-parent> <identifier>
                          <comma> <identifier> <close-parent>

<logical-atom>          : <true> | <false>
                        
<logical-op>            : <and>
                        
<identifier>            : [a-zA-Z][a-zA-Z0-9_]{0,127}

<true>                  : true
                        
<false>                 : false
                        
<and>                   : &&
                        
<not>                   : !
                        
<holds>                 : holds
                        
<member>                : memb
                        
<subset>                : subst
                        
<initially>             : initially
                        
<causes>                : causes

<implies>               : implies

<with>                  : with

<absence>               : absence

<always>                : always
                        
<if>                    : if
                        
<is>                    : is
                        
<after>                 : after

<ident>                 : ident

<sub-type>              : sub

<obj-type>              : obj

<acc-type>              : acc

<sub-grp-type>          : sub-grp

<obj-grp-type>          : obj-grp

<acc-grp-type>          : acc-grp
                        
<underscore>            : _

<open-parent>           : (

<close-parent>          : )

<comma>                 : ,
                        
<semicolon>             : ;
