-------------------------------------------------------------------------------
VLAD: V Logic-based Access-control Database
BNF for Policy Definition Parser

Vino Fernando Crescini  <jcrescin@cit.uws.edu.au>
-------------------------------------------------------------------------------
Started 2002 Dec 08
Updated 2005 Dec 10
-------------------------------------------------------------------------------

<start>                  : <program>

<program>                : <init> <body> <destroy>

<init>                   : <nil>

<body>                   : <entity-section> <initial-section>
                           <constraint-section> <update-section>

<destroy>                : <nil>

<entity-section>         : <nil> | <entity-stmt-list>

<initial-section>        : <nil> | <inital-stmt-list>

<constraint-section>     : <nil> | <constraint-stmt-list>

<update-section>         : <nil> | <update-stmt-list>

<entity-stmt-list>       : <entity-stmt> | <entity-stmt-list> <entity-stmt>

<initial-stmt-list>      : <initial-stmt> | <initial-stmt-list> <initial-stmt>

<constraint-stmt-list>   : <constraint-stmt> | <constraint-stmt-list>
                           <constraint-stmt>

<update-stmt-list>       : <update-stmt> | <update-stmt-list> <update-stmt>

<entity-stmt>            : <entity> <entity-declaration> <semicolon>

<entity-declaration>     : <sub-entity-decl> | <obj-entity-decl> |
                           <acc-entity-decl> | <sub-grp-entity-decl> |
                           <obj-grp-entity-decl> |
                           <acc-grp-entity-decl>

<sub-entity-decl>        : <sub-type> <sub-entity-list>

<acc-entity-decl>        : <acc-type> <acc-entity-list>

<obj-entity-decl>        : <obj-type> <obj-entity-list>

<sub-grp-entity-decl>    : <sub-grp-type> <sub-grp-entity-list>

<acc-grp-entity-decl>    : <acc-grp-type> <acc-grp-entity-list>

<obj-grp-entity-decl>    : <obj-grp-type> <obj-grp-entity-list>

<sub-entity-list>        : <identifier> | <sub-entity-list> <comma> <identifier>

<obj-entity-list>        : <identifier> | <obj-entity-list> <comma> <identifier>

<acc-entity-list>        : <identifier> | <acc-entity-list> <comma> <identifier>

<sub-grp-entity-list>    : <identifier> | <sub-grp-entity-list> <comma> <identifier>

<obj-grp-entity-list>    : <identifier> | <obj-grp-entity-list> <comma> <identifier>

<acc-grp-entity-list>    : <identifier> | <acc-grp-entity-list> <comma> <identifier>

<initial-stmt>           : <initially> <expression> <semicolon>

<constraint-stmt>        : <always> <expression> <implied-clause> <with-clause>
                           <semicolon>

<implied-clause>         : <nil> | <implied> <by> <expression>

<with-clause>            : <nil> | <with> <absence> <expression>

<with-clause>            : <nil> | <with> <absence> <expression>

<update-stmt>            : <identifier> <update-var-def> <causes> <expression>
                           <if-clause> <semicolon>

<if-clause>              : <nil> | <if> <expression>

<update-var-def>         : <open-parent> <close-parent> |
                           <open-parent> <update-var-list> <close-parent>

<update-var-list>        : <identifier> | <update-var-list> <comma> <identifier>

<expression>             : <expression> <logical-op> <boolean-fact> |
                           <boolean-fact>

<boolean-fact>           : <not> <fact> | <fact>

<fact>                   : <holds-fact> | <subst-fact> | <memb-fact>

<holds-fact>             : <holds> <open-parent> <identfier> <comma>
                           <identifier> <comma> <identifier> <close-parent>

<subst-fact>             : <subset> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>

<memb-fact>              : <member> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>

<logical-op>             : <and>

<identifier>             : <alpha> <alphanum> {0,127}

<and>                    : &&

<not>                    : !

<holds>                  : holds

<member>                 : memb

<subset>                 : subst

<initially>              : initially

<causes>                 : causes

<implied>                : implied

<by>                     : by

<with>                   : with

<absence>                : absence

<always>                 : always

<if>                     : if

<entity>                 : entity

<sub-type>               : sub

<obj-type>               : obj

<acc-type>               : acc

<sub-grp-type>           : sub-grp

<obj-grp-type>           : obj-grp

<acc-grp-type>           : acc-grp

<open-parent>            : (

<close-parent>           : )

<comma>                  : ,

<semicolon>              : ;

<underscore>             : _

<digit>                  : [0-9]

<number>                 : <digit> | <number> <digit>

<alpha>                  : [a-zA-Z]

<alphanum>               : <alpha> | <digit> | <underscore>
