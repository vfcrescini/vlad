-------------------------------------------------------------------------------
VLAD: V Logic-based Access-control Database
BNF for Policy Definition Parser

Vino Fernando Crescini  <jcrescin@cit.uws.edu.au>
-------------------------------------------------------------------------------
Started 2002 Dec 08
Updated 2006 Jan 11
-------------------------------------------------------------------------------

<start>                  : <program>

<program>                : <init> <body> <destroy>

<init>                   : <nil>

<body>                   : <identifier-section> <initial-section>
                           <relation-section> <constraint-section>
                           <update-section>

<destroy>                : <nil>

<identifier-section>     : <nil> | <identifier-stmt-list>

<initial-section>        : <nil> | <inital-stmt-list>

<relation-section>       : <nil> | <relation-stmt-list>

<constraint-section>     : <nil> | <constraint-stmt-list>

<update-section>         : <nil> | <update-stmt-list>

<identifier-stmt-list>   : <identifier-stmt> |
                           <identifier-stmt-list> <identifier-stmt>

<initial-stmt-list>      : <initial-stmt> | <initial-stmt-list> <initial-stmt>

<relation-stmt-list>     : <relation-stmt> |
                           <relation-stmt-list> <relation-stmt>

<constraint-stmt-list>   : <constraint-stmt> | <constraint-stmt-list>
                           <constraint-stmt>

<update-stmt-list>       : <update-stmt> | <update-stmt-list> <update-stmt>

<identifier-stmt>        : <entity> <entity-declaration> <semicolon> |
                           <interval> <interval-declaration> <semicolon>

<entity-declaration>     : <sub-entity-decl> | <obj-entity-decl> |
                           <acc-entity-decl> | <sub-grp-entity-decl> |
                           <obj-grp-entity-decl> |
                           <acc-grp-entity-decl>

<interval-declaration>   : <interval-decl> |
                           <interval-declaration> <comma> <interval-decl>

<sub-entity-decl>        : <sub-type> <sub-entity-list>

<acc-entity-decl>        : <acc-type> <acc-entity-list>

<obj-entity-decl>        : <obj-type> <obj-entity-list>

<sub-grp-entity-decl>    : <sub-grp-type> <sub-grp-entity-list>

<acc-grp-entity-decl>    : <acc-grp-type> <acc-grp-entity-list>

<obj-grp-entity-decl>    : <obj-grp-type> <obj-grp-entity-list>

<sub-entity-list>        : <identifier> | <sub-entity-list> <comma> <identifier>

<obj-entity-list>        : <identifier> | <obj-entity-list> <comma> <identifier>

<acc-entity-list>        : <identifier> | <acc-entity-list> <comma> <identifier>

<sub-grp-entity-list>    : <identifier> | <sub-grp-entity-list> <comma> <identifier>

<obj-grp-entity-list>    : <identifier> | <obj-grp-entity-list> <comma> <identifier>

<acc-grp-entity-list>    : <identifier> | <acc-grp-entity-list> <comma> <identifier>

<interval-decl>          : <identifier> <interval-enpoint-decl> |
                           <identifier>

<interval-endpoint-decl> : <open-bracket> <number> <comma> <number> <close-braket>

<initial-stmt>           : <initially> <expression> <semicolon>

<relation-stmt>          : <relation> <relation-list> <semicolon>

<relation-list>          : <relation-atom> |
                           <relation-list> <comma> <relation-atom>

<relation-atom>          : <rel-eql-atom> |
                           <rel-bef-atom> |
                           <rel-dur-atom> |
                           <rel-ovr-atom> |
                           <rel-met-atom> |
                           <rel-sta-atom> |
                           <rel-fin-atom>

<rel-eql-atom>           : <eql> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-bef-atom>           : <bef> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-dur-atom>           : <dur> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-ovr-atom>           : <ovr> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-met-atom>           : <met> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-sta-atom>           : <sta> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>
<rel-fin-atom>           : <fin> <open-parent> <identifier>
                           <comma> <identifier> <close-parent>

<constraint-stmt>        : <always> <expression> <implied-clause> <with-clause>
                           <where-clause> <semicolon>

<implied-clause>         : <nil> | <implied> <by> <expression>

<with-clause>            : <nil> | <with> <absence> <expression>

<with-clause>            : <nil> | <with> <absence> <expression>

<where-clause>           : <nil> | <where> <relation-list>

<update-stmt>            : <identifier> <update-var-def> <causes> <expression>
                           <if-clause> <where-clause> <semicolon>

<if-clause>              : <nil> | <if> <expression>

<update-var-def>         : <open-parent> <close-parent> |
                           <open-parent> <update-var-list> <close-parent>

<update-var-list>        : <identifier> | <update-var-list> <comma> <identifier>

<expression>             : <expression> <logical-op> <boolean-fact> |
                           <boolean-fact>

<boolean-fact>           : <not> <fact> | <fact>

<fact>                   : <holds-fact> | <subst-fact> | <memb-fact>

<holds-fact>             : <holds> <open-parent> <identfier> <comma>
                           <identifier> <comma> <identifier> <comma>
                           <identifier> <close-parent>

<subst-fact>             : <subset> <open-parent> <identifier>
                           <comma> <identifier> <comma> <identifier>
                           <close-parent>

<memb-fact>              : <member> <open-parent> <identifier>
                           <comma> <identifier> <comma> <identifier>
                           <close-parent>

<logical-op>             : <and>

<identifier>             : <alpha> <alphanum> {0,126}

<and>                    : &&

<not>                    : !

<holds>                  : holds

<member>                 : memb

<subset>                 : subst

<initially>              : initially

<relation>               : relation

<causes>                 : causes

<implied>                : implied

<by>                     : by

<with>                   : with

<absence>                : absence

<where>                  : where

<always>                 : always

<if>                     : if

<entity>                 : entity

<interval>               : interval

<sub-type>               : sub

<obj-type>               : obj

<acc-type>               : acc

<sub-grp-type>           : sub-grp

<obj-grp-type>           : obj-grp

<acc-grp-type>           : acc-grp

<eql>                    : equals

<bef>                    : before

<dur>                    : during

<ovr>                    : overlaps

<met>                    : meets

<sta>                    : starts

<fin>                    : finishes

<open-parent>            : (

<close-parent>           : )

<open-bracker>           : [

<close-braket>           : ]

<comma>                  : ,

<semicolon>              : ;

<underscore>             : _

<digit>                  : [0-9]

<number>                 : <digit>{1,127}

<alpha>                  : [a-zA-Z]

<alphanum>               : <alpha> | <digit> | <underscore>
